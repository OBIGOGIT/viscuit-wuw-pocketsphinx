plugins {
    id 'com.android.library'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def pocketsphinx_dir = properties.getProperty('pocketsphinx.dir')
def sphinxbase_dir = properties.getProperty('sphinxbase.dir')

android {
    namespace 'edu.cmu.pocketsphinx'
    compileSdk 31
    ndkVersion '25.2.9519653'

    defaultConfig {
        minSdk 29
        targetSdk 31
        versionCode 5
        versionName "1.0.0"
        setProperty("archivesBaseName", "pocketsphinx-android-$versionName")
        externalNativeBuild {
            cmake {
                arguments "-DPOCKETSPHINX_DIR=$pocketsphinx_dir", "-DSPHINXBASE_DIR=$sphinxbase_dir"
            }
        }
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
         main {
             java.srcDirs = ['src/main/java', 'build/generated-src']
         }
    }
    externalNativeBuild {
        cmake {
             path "CMakeLists.txt"
        }
    }
    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }
}

println "module name=$name"
println "build dir=$buildDir"

task swig() {
    doFirst {
        println "sphinxbase dir=$sphinxbase_dir"
        println "pocketsphinx dir=$pocketsphinx_dir"
        assert file("$sphinxbase_dir/include").exists()
        assert file("$pocketsphinx_dir/include").exists()
    }
    doLast {
        mkdir 'build/generated-src/java'
        mkdir 'build/generated-src/cpp'

        exec {
            commandLine 'swig',
                    "-I$sphinxbase_dir/include",
                    "-I$sphinxbase_dir/swig",
                    "-java", "-package", "edu.cmu.pocketsphinx",
                    "-outdir", "build/generated-src/java",
                    "-o", "build/generated-src/cpp/sphinxbase_wrap.c",
                    "$sphinxbase_dir/swig/sphinxbase.i"
        }
        exec {
            commandLine 'swig',
                    "-I$sphinxbase_dir/swig",
                    "-I$pocketsphinx_dir/include",
                    "-I$pocketsphinx_dir/swig",
                    "-java", "-package", "edu.cmu.pocketsphinx",
                    "-outdir", "build/generated-src/java", "-o", "build/generated-src/cpp/pocketsphinx_wrap.c",
                    "$pocketsphinx_dir/swig/pocketsphinx.i"
        }
    }
}

preBuild.dependsOn swig
